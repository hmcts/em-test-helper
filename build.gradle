plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.6.2'
    id 'com.github.ben-manes.versions' version '0.41.0'
    id 'java-library'
    id 'maven-publish'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

apply plugin: 'idea'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber
sourceCompatibility = 11
targetCompatibility = 11

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntimeOnly.extendsFrom(testRuntimeOnly)
}

sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java.srcDir 'src/test/java'
    }
}

checkstyle {
    //    ignoreFailures = true
    sourceSets = []
}
compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/em-test-helper"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/em-test-helper"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'em-test-helper'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Em Test Helper')
                root.appendNode('name', 'Em Test Helper')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

ext {
    lombokVersion = '1.18.20'
    feignVersion = '11.2'
    feignFormVersion = '3.8.0'
    wiremockVersion = '2.24.1'
    junitJupiterVersion = '5.8.1'
}

ext["spring-cloud-openfeign.version"] = "3.0.3"

dependencies {
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.0'
    implementation "com.github.hmcts:idam-java-client:2.0.1"
    implementation "com.github.hmcts:ccd-client:4.9.0_HMAN-319"
    implementation "com.github.hmcts:document-management-client:7.0.1"
    implementation 'com.github.hmcts:ccd-case-document-am-client:1.59'

    implementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: feignFormVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.13.0'

    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.0'

    testImplementation group: 'com.github.tomakehurst', name:'wiremock-jre8', version: '2.32.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'

    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.1.0'
    implementation group: 'junit', name: 'junit', version: '4.13'
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'org.apache.logging.log4j', version: '2.17.0') {
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }
    }

}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
}

bootJar {
    archiveClassifier = 'boot'
    enabled = false
}

jar {
    archiveClassifier = ''
    enabled = true
}

idea {
    module {
        testSourceDirs += project.sourceSets.aat.java.srcDirs
        testResourceDirs += project.sourceSets.aat.resources.srcDirs
    }
}


task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/functional/**"
}
