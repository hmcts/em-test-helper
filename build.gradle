plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.5.3'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'idea'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

configurations {
    aatImplementation.extendsFrom(testImplementation)
    aatRuntimeOnly.extendsFrom(testRuntimeOnly)
}

sourceSets {
    aat {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
    test {
        java.srcDir 'src/test/java'
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.25.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}
compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/em-test-helper"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/em-test-helper"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }

    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId 'em-test-helper'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Em Test Helper')
                root.appendNode('name', 'Em Test Helper')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

ext {
    lombokVersion = '1.18.38'
    feignVersion = '13.6'
    feignFormVersion = '3.8.0'
    wiremockVersion = '2.24.1'
    junitJupiterVersion = '5.13.2'
}

ext["spring-cloud-openfeign.version"] = "3.0.3"

dependencies {
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.3.0'
    api "com.github.hmcts:idam-java-client:3.0.4"
    api "com.github.hmcts:core-case-data-store-client:5.1.1"
    api "com.github.hmcts:document-management-client:7.0.0"
    api 'com.github.hmcts:ccd-case-document-am-client:1.59.2'

    implementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.3.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: feignFormVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.19.1'

    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.3.0'

    testImplementation group: 'com.github.tomakehurst', name:'wiremock-jre8', version: '3.0.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.18.0'

    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '4.3.0'
    implementation group: 'junit', name: 'junit', version: '4.13.2'
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'org.apache.logging.log4j', version: '2.25.0') {
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }
        dependencySet(group: 'org.junit.platform', version: '1.13.1') {
            entry 'junit-platform-commons'
            entry 'junit-platform-engine'
            entry 'junit-platform-launcher'
        }
    }

}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

test {
    useJUnitPlatform()
}

bootJar {
    archiveClassifier = 'boot'
    enabled = false
}

jar {
    archiveClassifier = ''
    enabled = true
}

idea {
    module {
        testSourceDirs += project.sourceSets.aat.java.srcDirs
        testResourceDirs += project.sourceSets.aat.resources.srcDirs
    }
}


task functional(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes functional tests'
    setTestClassesDirs(sourceSets.aat.output.classesDirs)
    setClasspath(sourceSets.aat.runtimeClasspath)
    include "uk/gov/hmcts/reform/em/functional/**"
    useJUnitPlatform()
}
